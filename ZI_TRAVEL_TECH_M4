@AbapCatalog.viewEnhancementCategory: [#NONE]
@AccessControl.authorizationCheck: #NOT_REQUIRED
@EndUserText.label: 'ROOT ENTITY  FOR TRAVEL'
@Metadata.ignorePropagatedAnnotations: true

@ObjectModel.usageType:{
    serviceQuality: #X,
    sizeCategory: #S,
    dataClass: #MIXED
} 
define root view entity ZI_TRAVEL_TECH_M4 
as select from ztravel_tech_m4
composition[0..*] of ZI_BOOKING_TECH_M4 as _Booking
 association[0..1] to /DMO/I_Agency as _agency on $projection.AgencyId = _agency.AgencyID
 association[0..1] to /DMO/I_Customer as _customer on $projection.CustomerId = _customer.CustomerID
 association[1..1] to  I_Currency  as _currency  on $projection.CurrencyCode = _currency.Currency
 association[1..1] to /DMO/I_Overall_Status_VH as _STATUS on $projection.OverallStatus = _STATUS.OverallStatus
{
    key travel_id as TravelId,
    agency_id as AgencyId,
    customer_id as CustomerId,
    begin_date as BeginDate,
    end_date as EndDate,
    @Semantics.amount.currencyCode: 'CurrencyCode'
    booking_fee as BookingFee,
    @Semantics.amount.currencyCode: 'CurrencyCode'
    total_price as TotalPrice,
    currency_code as CurrencyCode,
    description as Description,
    overall_status as OverallStatus,
    created_by as CreatedBy,
    created_at as CreatedAt,
    last_changed_by as LastChangedBy,
//    the persistent field local_last_changed_at palys a special role as a field ETag.
    @Semantics.systemDateTime.localInstanceLastChangedAt: true
    last_changed_at as LastChangedAt,
    _Booking,
    _agency , //We expose annotations so that we can carefully choose what metadata becomes visible to UI apps or service consumers, while keeping internal details hidden.
    _customer,
    _currency,
    _STATUS
}
